/**
 * Root Fusion template for the Therapie Ochsenhausen site
 */

root {
//    default {
//        type = ${q(node).property('_nodeType') + '.Document'}
//        renderPath >
//    }
    documentType {
        condition = Neos.Fusion:CanRender {
            type = ${q(documentNode).property('_nodeType.name') + '.Document'}
        }
        type = ${q(documentNode).property('_nodeType.name') + '.Document'}
    }
}

prototype(Neos.NodeTypes:Page.Document) >
prototype(Neos.NodeTypes:Page.Document) < prototype(Neos.Neos:Page) {
	head {
		stylesheets.site = Neos.Fusion:Template {
			templatePath = 'resource://Js.TherapieOx/Private/Templates/Page/Default.html'
			sectionName = 'stylesheets'
		}

		javascripts.site = Neos.Fusion:Template {
			templatePath = 'resource://Js.TherapieOx/Private/Templates/Page/Default.html'
			sectionName = 'headScripts'
		}
	}

	@context {
		bgImageUri = Neos.Neos:ImageUri {
			asset = ${q(node).property('backgroundimage')}
		}
		bgRootImageUri = Neos.Neos:ImageUri {
			asset = ${q(site).property('backgroundimage')}
		}
	}
	bodyTag {
		attributes.style.@process {
			backgroundImage = ${value + ' ' + 'background-image: url(' + bgImageUri + ');'}
	   		backgroundImage.@if.backgroundImageIsSet = ${ q(node).property('backgroundimage') == '' ? false : true }

			backgroundImageRoot = ${value + ' ' + 'background-image: url(' + bgRootImageUri + ');'}
	   		backgroundImageRoot.@if {
	   			backgroundImageIsSet = ${ q(node).property('backgroundimage') == '' ? true : false }
	   			backgroundImageIsSet = ${ q(site).property('backgroundimage') == '' ? false : true }
	   		}
	   	}
	   attributes.class = ${q(node).property('area')}
	   #attributes.class.@process.area.@if.areaIsSet
	}

	body {
		templatePath = 'resource://Js.TherapieOx/Private/Templates/Page/Default.html'
		sectionName = 'body'

		page.title = ${q(site).property('siteheadline')}
		svglogo = ${Configuration.setting('Js.TherapieOx.Logo_SVG')}

		parts {
			menu = Js.TherapieOx:Menu {
				templatePath = 'resource://Js.TherapieOx/Private/Templates/FusionObjects/MainMenu.html'
			    #entryLevel = 2
			    maximumLevels = 1
			    # Only 3 items
			    itemCollection = ${q(site).children('[instanceof Neos.Neos:Document]').get()}
			    itemCollection.@process.keepOnlyTheFirstTwoItems = ${q(value).slice(0,3).get()}
			}
			togglemenu = Js.TherapieOx:Menu {
				templatePath = 'resource://Js.TherapieOx/Private/Templates/FusionObjects/ToggleMenu.html'
			    #entryLevel = 1
			    maximumLevels = 3
			}

			mobilemenu = Js.TherapieOx:Menu {
				templatePath = 'resource://Js.TherapieOx/Private/Templates/FusionObjects/MobileMenu.html'
			    maximumLevels = 3
			}

			footermenu = Neos.Neos:Menu
		    footermenu {
		        #entryLevel = 1
		        templatePath = 'resource://Js.TherapieOx/Private/Templates/FusionObjects/FooterMenu.html'
		        maximumLevels = 1
		        startingPoint = ${q(site).children('[uriPathSegment="meta"]').get(0)}
		    }
		}

		// These are your content areas, you can define as many as you want, just name them and the nodePath.
		content {
			// The default content section
			main = Neos.Neos:PrimaryContent {
				nodePath = 'main'
			}
		}

		copyright = ${'&copy; ' + Date.year(Date.now()) + ' ' + String.stripTags(q(site).property('siteheadline'))}

		javascripts.site = Neos.Fusion:Template {
			templatePath = 'resource://Js.TherapieOx/Private/Templates/Page/Default.html'
			sectionName = 'bodyScripts'
		}
	}
}

# Menu with node properties
prototype(Js.TherapieOx:Menu) >
prototype(Js.TherapieOx:Menu) < prototype(Neos.Neos:Menu)
//prototype(Js.TherapieOx:Menu).@class = 'Js\\TherapieOx\\Fusion\\MenuImplementation'

prototype(Js.TherapieOx:PortalPage.Document) < prototype(Neos.NodeTypes:Page.Document)
prototype(Js.TherapieOx:HomePage.Document) < prototype(Neos.NodeTypes:Page.Document)

prototype(Js.TherapieOx:Areabox) < prototype(Neos.Neos:Content) {
    boxContent = Neos.Neos:ContentCollection
    boxContent {
            nodePath = 'boxContent'
    }
}

prototype(Js.TherapieOx:Twocolumns) < prototype(Neos.Neos:Content) {
    templatePath = 'resource://Js.TherapieOx/Private/Templates/NodeTypes/Twocolumns.html'

    column0Content = Neos.Neos:ContentCollection
    column0Content {
            nodePath = 'column0'
    }
    column1Content = Neos.Neos:ContentCollection
    column1Content {
            nodePath = 'column1'
    }
}

prototype(Neos.NodeTypes:Image) {
	maximumWidth = 610
}
